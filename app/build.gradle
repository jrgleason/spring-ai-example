plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'idea'
    id "com.github.node-gradle.node" version "7.1.0"
}

group = 'jrg.example'
version = '0.0.3-SNAPSHOT'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

node {
    download = true
    version = "22.14.0"
    npmVersion = "11.3.0"
    nodeProjectDir = file("${project.projectDir}/../ui")
}

repositories {
    mavenCentral()
}

ext {
    set('springAiVersion', "1.0.0-M6")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-integration'
    implementation 'org.springframework.integration:spring-integration-mqtt:6.3.4'

    implementation "org.springframework.ai:spring-ai-openai-spring-boot-starter"
    implementation "org.springframework.ai:spring-ai-pinecone-store-spring-boot-starter"
    implementation 'org.springframework.boot:spring-boot-starter-websocket:3.3.5'

    implementation 'org.apache.httpcomponents.client5:httpclient5:5.4.2'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.github.ben-manes.caffeine:caffeine'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'

    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.10.3')
        }
    }
}

// Create a custom task for 'npm run build'
tasks.register('npmBuild', NpmTask) {
    // Properly reference the npmInstall task
    dependsOn(tasks.named('npmInstall'))

    // Set the arguments using the proper method
    setArgs(['run', 'build'])
}

bootRun {
    dependsOn npmBuild
}
build {
    dependsOn npmBuild
}